apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'application'

version = '0.1.0'
sourceCompatibility = '1.7'

project.ext {
    dropwizardVersion = '0.7.1'
    dropWizardConfig = './src/dist/config/config.yml'
}

mainClassName = 'com.kevinm416.report.server.ReportApplication'

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-migrations:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-auth:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-jdbi:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-assets:' + dropwizardVersion
    compile 'org.apache.shiro:shiro-web:1.2.3'
    compile 'org.postgresql:postgresql:9.3-1102-jdbc41'
    compile 'com.h2database:h2:1.3.168'
}

run {
    args = ['server', dropWizardConfig]
}

task migrate(type: JavaExec, dependsOn:[classes]) {
    main = mainClassName
    args = ['db', 'migrate', dropWizardConfig]
    classpath = sourceSets.main.runtimeClasspath
}

task dbStatus(type: JavaExec, dependsOn:[classes]) {
    main mainClassName
    args = ['db', 'status', dropWizardConfig]
    classpath = sourceSets.main.runtimeClasspath
}

task dbClean(type: JavaExec, dependsOn:[classes]) {
    main mainClassName
    args = ['db', 'drop-all', '--confirm-delete-everything', dropWizardConfig]
    classpath = sourceSets.main.runtimeClasspath
}


task hydrate(dependsOn:[classes, dbClean, migrate]) {
}

task dbDoc(type: JavaExec, dependsOn:[classes]) {
    main mainClassName
    args = ['db', 'generate-docs', dropWizardConfig, './db-docs']
    classpath = sourceSets.main.runtimeClasspath
}

task dbCheck(type: JavaExec, dependsOn:[classes]) {
    main mainClassName
    args = ['db', 'status', dropWizardConfig]
    classpath = sourceSets.main.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
